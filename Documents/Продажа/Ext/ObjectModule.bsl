
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СебестоимостьКотятОстатки.Коробка КАК Коробка,
		|	СебестоимостьКотятОстатки.Порода КАК Порода,
		|	СУММА(ЕСТЬNULL(СебестоимостьКотятОстатки.СебестоимостьОстаток, 0)) КАК Себестоимость,
		|	ЕСТЬNULL(ВложенныйЗапрос.КоличествоОстаток, 0) КАК Количество
		|ИЗ
		|	РегистрНакопления.СебестоимостьКотят.Остатки(
		|			&МоментВремени,
		|			Коробка В (&Коробка)
		|				И Порода В (&Порода)) КАК СебестоимостьКотятОстатки
		|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ОстаткиКотятВКоробкахОстатки.Коробка КАК Коробка,
		|			ОстаткиКотятВКоробкахОстатки.Порода КАК Порода,
		|			СУММА(ОстаткиКотятВКоробкахОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|		ИЗ
		|			РегистрНакопления.ОстаткиКотятВКоробках.Остатки(
		|					&МоментВремени,
		|					Коробка В (&Коробка)
		|						И Порода В (&Порода)) КАК ОстаткиКотятВКоробкахОстатки
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ОстаткиКотятВКоробкахОстатки.Коробка,
		|			ОстаткиКотятВКоробкахОстатки.Порода) КАК ВложенныйЗапрос
		|		ПО СебестоимостьКотятОстатки.Коробка = ВложенныйЗапрос.Коробка
		|			И СебестоимостьКотятОстатки.Порода = ВложенныйЗапрос.Порода
		|
		|СГРУППИРОВАТЬ ПО
		|	СебестоимостьКотятОстатки.Коробка,
		|	СебестоимостьКотятОстатки.Порода,
		|	ЕСТЬNULL(ВложенныйЗапрос.КоличествоОстаток, 0)";
	
	Запрос.УстановитьПараметр("Коробка", Товары.ВыгрузитьКолонку("Коробка"));
	Запрос.УстановитьПараметр("Порода", Товары.ВыгрузитьКолонку("Порода"));
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
		
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Движения.СебестоимостьКотят.Записывать = Истина;
	Движения.ОстаткиКотятВКоробках.Записывать = Истина;
	Движения.ПродажиКотят.Записывать = Истина;
	
	Для Каждого ТекСтрокаТовары Из Товары Цикл 
		
		МассивСтрокСебестоимости = РезультатЗапроса.НайтиСтроки(Новый Структура("Коробка, Порода", ТекСтрокаТовары.Коробка, ТекСтрокаТовары.Порода));
		
		Если МассивСтрокСебестоимости.Количество() = 0 Или МассивСтрокСебестоимости[0].Количество < ТекСтрокаТовары.Количество Тогда
			Отказ = Истина;  
			Сообщить(СтрШаблон("Недостаточно котят породы %1 в коробке %2!", ТекСтрокаТовары.Порода, ТекСтрокаТовары.Коробка));  
			Продолжить;
		КонецЕсли;
		
		Себестоимость = МассивСтрокСебестоимости[0].Себестоимость * ТекСтрокаТовары.Количество;
		
		Движение = Движения.СебестоимостьКотят.Добавить();
		Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
		Движение.Период 		= Дата;
		Движение.Коробка 		= ТекСтрокаТовары.Коробка;
		Движение.Порода 		= ТекСтрокаТовары.Порода;
		Движение.Себестоимость 	= Себестоимость;

		Движение = Движения.ОстаткиКотятВКоробках.Добавить();
		Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
		Движение.Период 		= Дата;
		Движение.Коробка 		= ТекСтрокаТовары.Коробка;
		Движение.Порода 		= ТекСтрокаТовары.Порода;
		Движение.Количество 	= ТекСтрокаТовары.Количество; 
		
		Движение = Движения.ПродажиКотят.Добавить();
		Движение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
		Движение.Период 		= Дата;
		Движение.Коробка 		= ТекСтрокаТовары.Коробка;
		Движение.Порода 		= ТекСтрокаТовары.Порода;
		Движение.Количество 	= ТекСтрокаТовары.Количество;
		Движение.СуммаПродажи 	= ТекСтрокаТовары.Сумма;
		Движение.Себестоимость 	= Себестоимость;
		
	КонецЦикла;
	
КонецПроцедуры
